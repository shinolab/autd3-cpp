cmake_minimum_required(VERSION 3.21)

if(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
  message(FATAL_ERROR "In-source builds not allowed. Please make a new directory (called a build directory) and run CMake from there. You may need to remove CMakeCache.txt. ")
endif()

project(autd3 VERSION 30.0.1)

message(STATUS "Build AUTD3: ${PROJECT_VERSION}")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(CMAKE_POLICY_DEFAULT_CMP0077 NEW)

option(USE_SYSTEM_EIGEN "Use system installed eigen3" OFF)
option(USE_SYSTEM_LIBCORO "Use system installed libcoro" OFF)
option(AUTD3_USE_PCH "Use precompiled headers" OFF)

if(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.24.0") 
  cmake_policy(SET CMP0135 NEW)
endif()

include(FetchContent)
if(USE_SYSTEM_EIGEN)
  find_package(Eigen3 REQUIRED)
else()
  set(EIGEN_BUILD_DOC OFF)
  set(BUILD_TESTING OFF)
  set(EIGEN_BUILD_PKGCONFIG OFF)
  FetchContent_Declare(
    eigen
    GIT_REPOSITORY https://gitlab.com/libeigen/eigen.git
    GIT_TAG 3.4.0)
  FetchContent_MakeAvailable(eigen)
endif()

add_library(autd3_common INTERFACE)
target_include_directories(autd3_common INTERFACE ${PROJECT_SOURCE_DIR}/include)
target_link_libraries(autd3_common INTERFACE Eigen3::Eigen)
target_compile_definitions(autd3_common INTERFACE EIGEN_MPL2_ONLY)
target_link_directories(autd3_common INTERFACE ${PROJECT_SOURCE_DIR}/lib)
if(MSVC)
  target_compile_options(autd3_common INTERFACE /wd4819)
  target_compile_options(autd3_common INTERFACE /wd4324)
endif()
if(WIN32)
  target_compile_definitions(autd3_common INTERFACE NOMINMAX)
endif()
if(AUTD3_USE_PCH)
  file(GLOB_RECURSE autd3_headers ${PROJECT_SOURCE_DIR}/include/**/*.hpp)
  target_precompile_headers(autd3_common INTERFACE ${autd3_headers})
endif()
add_library(autd3::common ALIAS autd3_common)

add_library(autd3 INTERFACE)
target_link_libraries(autd3 INTERFACE autd3::common autd3capi)
if(AUTD3_USE_PCH)
  target_precompile_headers(autd3 REUSE_FROM autd3::common)
endif()
if(WIN32)
  target_link_libraries(autd3 INTERFACE ntdll.lib Winmm.lib bcrypt.lib userenv.lib RuntimeObject.lib Propsys.lib)
elseif(APPLE)
  target_link_libraries(autd3 INTERFACE "-framework CoreFoundation")
endif()
add_library(autd3::autd3 ALIAS autd3)

add_library(autd3_gain_holo INTERFACE)
target_link_libraries(autd3_gain_holo INTERFACE autd3::common autd3capi_gain_holo)
if(AUTD3_USE_PCH)
  target_precompile_headers(autd3_gain_holo REUSE_FROM autd3::common)
endif()
add_library(autd3::gain::holo ALIAS autd3_gain_holo)

add_library(autd3_modulation_audio_file INTERFACE)
target_link_libraries(autd3_modulation_audio_file INTERFACE autd3::common autd3capi_modulation_audio_file)
if(AUTD3_USE_PCH)
  target_precompile_headers(autd3_modulation_audio_file REUSE_FROM autd3::common)
endif()
add_library(autd3::modulation::audio_file ALIAS autd3_modulation_audio_file)

add_library(autd3_link_twincat INTERFACE)
target_link_libraries(autd3_link_twincat INTERFACE autd3::common autd3capi_link_twincat)
if(AUTD3_USE_PCH)
  target_precompile_headers(autd3_link_twincat REUSE_FROM autd3::common)
endif()
if(WIN32)
  target_link_libraries(autd3_link_twincat INTERFACE ws2_32)
endif()
add_library(autd3::link::twincat ALIAS autd3_link_twincat)

add_library(autd3_link_simulator INTERFACE)
target_link_libraries(autd3_link_simulator INTERFACE autd3::common autd3capi_link_simulator)
if(AUTD3_USE_PCH)
  target_precompile_headers(autd3_link_simulator REUSE_FROM autd3::common)
endif()
if(WIN32)
  target_link_libraries(autd3_link_simulator INTERFACE ws2_32)
endif()
add_library(autd3::link::simulator ALIAS autd3_link_simulator)
